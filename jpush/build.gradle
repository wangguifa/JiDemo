plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.huawei.agconnect'
    id 'maven-publish'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    afterEvaluate {
        publishing {
            def versionName = "0.0.1" // 当前版本依赖库版本号，这个jitpack不会使用到，只是我们开发者自己查看
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    from components.release // 表示发布 release（jitpack 都不会使用到）
                    // You can then customize attributes of the publication as shown below.
                    groupId = 'com.guifa.jpush' // 这个是依赖库的组 id
                    artifactId = 'jpush' // 依赖库的名称（jitpack 都不会使用到）
                    version = versionName
                }
            }
            repositories {
                // 下面这部分，不是很清楚加不加，但是最后加上
                maven {
                    // change URLs to point to your repos, e.g. http://my.org/repo
                    def baseUrl = buildDir.getParent()
                    def releasesRepoUrl = "$baseUrl/repos/releases"
                    def snapshotsRepoUrl = "$baseUrl/repos/snapshots"
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    // 此处以JCore 3.2.4 版本为例。
    api 'cn.jiguang.sdk:jcore:3.2.4'
    // 此处以JPush 4.6.4 版本为例
    api 'cn.jiguang.sdk:jpush:4.6.4'

    // 若不集成厂商通道，可直接跳过以下依赖
    // 接入华为厂商
    api 'com.huawei.hms:push:6.1.0.300'
    //仅使用 com.huawei.hms:push:6.1.0.XXX 版本需要加 base 依赖，其他版本不需要
    api 'com.huawei.hms:base:6.2.0.300'

    // 极光厂商插件版本与接入 JPush 版本保持一致，下同
    api 'cn.jiguang.sdk.plugin:huawei:4.6.4'
    // 接入 FCM 厂商
    api 'com.google.firebase:firebase-messaging:23.0.0'
    api 'cn.jiguang.sdk.plugin:fcm:4.6.4'
    // 接入魅族厂商
    api 'cn.jiguang.sdk.plugin:meizu:4.6.4'
    // 接入 VIVO 厂商
    api 'cn.jiguang.sdk.plugin:vivo:4.6.4'
    // 接入小米厂商
    api 'cn.jiguang.sdk.plugin:xiaomi:4.6.4'
    // 接入 OPPO 厂商
    api 'cn.jiguang.sdk.plugin:oppo:4.6.4'
    // oppo 厂商 aar 需要单独引入，请将 jpush-android-xxx-release/third-push/oppo/libs 下 com.heytap.msp-push-x.x.x.aar 单独拷贝一份到应用 module/libs 下
    api(name: 'com.heytap.msp-push-3.0.0', ext: 'aar')
    //以下为 OPPO 3.0.0 aar需要依赖
    api 'com.google.code.gson:gson:2.8.7'
    api 'commons-codec:commons-codec:1.10'
    api 'androidx.annotation:annotation:1.3.0'
}